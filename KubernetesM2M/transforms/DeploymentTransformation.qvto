import PrimativeDataTypesHelper;
import Factory;
library DeploymentTransformation;


modeltype UML uses 'http://www.eclipse.org/uml2/5.0.0/UML';
modeltype KuberntesMetaModel uses 'http://www.example.org/kubernetesModel';
modeltype kuberProfile uses 'http://kubernetesProfile';

/**
* Return a deployment that has standard attributes for mysql.
**/

helper UML::Node::mysqlToDeployment(inout deployment : KuberntesMetaModel::Deployment, in mysqlStereoType :UML::Stereotype,inout infra : KuberntesMetaModel::Infrastructure )  {
		deployment.name := self.name;
		deployment.port := 3306;
		deployment.replicas := 1;	
		deployment.image := "mysql:"+self.getString(mysqlStereoType,"version");
		var rootPassword := self.getValue(mysqlStereoType,"rootPassword").oclAsType(kuberProfile::DataTypes::RootPassword);
		var dataBaseName := self.getValue(mysqlStereoType,"dataBase").oclAsType(kuberProfile::DataTypes::MySqlDatabase);
		var mySqlDumpFile := self.getValue(mysqlStereoType,"mysqlDumpFile").oclAsType(kuberProfile::DataTypes::MySQLDumpfile);
		
		
		var pvcName :=self.name + "-pvc";
		var volumeName := self.name + "-persistent-storage";		
		
		var pvc := createPersistentVolumeClaim(self.getInteger(mysqlStereoType,"storageSizeInGB"),pvcName);
	
		
		var volume := createVolume(volumeName,pvcName,KuberntesMetaModel::VolumeType::persistentVolumeClaim);
		var volumeMount := createVolumeMount(volumeName,"/var/lib/mysql");
	 		
		//var volumeMount := new VolumeMount();
 		
		deployment.volumes += volume;
		deployment.volumeMounts += volumeMount;
	 		
	 	
	 	if(rootPassword <> null){
	 		deployment.enviromentVariables += createEnviromentVariable("MYSQL_ROOT_PASSWORD",rootPassword.passsword);
	 	 };
	 	if(dataBaseName <> null){	 
	 		deployment.enviromentVariables += createEnviromentVariable("MYSQL_DATABASE",dataBaseName.name);
	 	};	
	 	if(mySqlDumpFile <> null){
	
	 		//var configMap := new ConfigMap();
	 		var configMapName := self.name + "-sql-dump";
	 		
	 		var configMap := createConfigMap(configMapName,true,mySqlDumpFile.filePath);

	 		
	 		infra.objectsCount := infra.objectsCount +1;
	 		configMap.id := infra.objectsCount;
	 		infra.configMaps += configMap;
	 		
	 		//var cVolume := new Volume();
	 		
	 		var cVolumeName := "usermanagement-dbcreation-script";
	 		var cVolume := createVolume(cVolumeName,configMapName,KuberntesMetaModel::VolumeType::configMap);
	 		 		
	 		//var cVolumeMount := new VolumeMount();
	 		var cVolumeMount := createVolumeMount(cVolumeName,"/docker-entrypoint-initdb.d");
	 	

	 		deployment.volumeMounts += cVolumeMount;
	 		deployment.volumes += cVolume;
	 		
	 	};
		infra.objectsCount := infra.objectsCount +1;
		pvc.id := infra.objectsCount;
		infra.persistentVolumeClaims += pvc;	
	
}
helper UML::Node::mongoToDeployment(inout deployment : KuberntesMetaModel::Deployment, in mongoStereotype :UML::Stereotype, inout infra : KuberntesMetaModel::Infrastructure )  {
	deployment.name := self.name;
	deployment.port := 27017;
	deployment.replicas := 1;
	deployment.image := "mongo";
	var dataBaseUser := self.getValue(mongoStereotype,"databaseUser").oclAsType(kuberProfile::DataTypes::DatabaseUser);
	var pvcName :=self.name + "-pvc";
	var volumeName := self.name + "-persistent-storage";		
		
	var pvc := createPersistentVolumeClaim(self.getInteger(mongoStereotype,"storageSizeInGB"),pvcName);
	
		
	var volume := createVolume(volumeName,pvcName,KuberntesMetaModel::VolumeType::persistentVolumeClaim);
	var volumeMount := createVolumeMount(volumeName,"/data/db/");
	
	if(dataBaseUser <> null){
		deployment.enviromentVariables += createEnviromentVariable("MONGO_INITDB_ROOT_USERNAME",dataBaseUser.userName);
		deployment.enviromentVariables += createEnviromentVariable("MONGO_INITDB_ROOT_PASSWORD",dataBaseUser.password);
	};
	
	infra.objectsCount := infra.objectsCount +1;
	pvc.id := infra.objectsCount;
	infra.persistentVolumeClaims += pvc;	
	log("asd");
}


helper UML::Node::deploymentToDeployment(inout deployment: KuberntesMetaModel::Deployment, in deploymentStereoType : UML::Stereotype, inout infra : KuberntesMetaModel::Infrastructure ){
	    deployment.image := self.getString(deploymentStereoType,"image");
	    deployment.port := self.getInteger(deploymentStereoType,"port");
	    deployment.replicas := self.getInteger(deploymentStereoType,"replicas");
	    deployment.name := self.name;
	    deployment.dataPersistent := self.getDataPersistent(deploymentStereoType);
	    deployment.enviromentVariables += self.getEnviromentVariables(deploymentStereoType);	  	  	
	  
	 	var dataPersistent :=	self.getValue(deploymentStereoType,"dataPersistent").oclAsType(kuberProfile::DataTypes::PersistentData);
	 	
	 	if(dataPersistent <> null){
	 		log("asd");
	 		//var pvc := new PersistentVolumeClaim();
	 		var pvcName :=self.name + "-pvc";
	 		var pvc := createPersistentVolumeClaim(dataPersistent.sizeInGB, pvcName);
	 		
	 	
	 		var volumeName := self.name + "-persistent-storage";
	 		deployment.volumes += createVolume(volumeName,pvcName,KuberntesMetaModel::VolumeType::persistentVolumeClaim);
	 	
	 		//var volumeMount := new VolumeMount();
	 		deployment.volumeMounts += createVolumeMount(volumeName,dataPersistent.mountPath);
	 			
	 		
	 		infra.objectsCount := infra.objectsCount +1;
			pvc.id := infra.objectsCount;
	 		infra.persistentVolumeClaims += pvc;		
	 	
	 		
	 	};
	  	if(deploymentStereoType.name = "AdvancedDeployment"){	  	
	  		deployment.securityContext := self.getSecurityContext(deploymentStereoType);
	  		deployment.resourceAllocation := self.getResourceAllocation(deploymentStereoType);
	  		deployment.args := self.getArguments(deploymentStereoType);
	  		deployment.command := self.getCommand(deploymentStereoType);
	  		deployment.memoryMount := self.getMemoryMount(deploymentStereoType);
	  		deployment.additionalPorts := self.getAdditionalPorts(deploymentStereoType);
	  	}; 	
	  
	    
	  
}

helper UML::Node::getAdditionalPorts(in deploymentStereoType: UML::Stereotype): OrderedSet(KuberntesMetaModel::Port){
	
	var ports: Set(KuberntesMetaModel::Port);
	self.getValue(deploymentStereoType,"additionalPorts")->flatten()->asSequence()->forEach(port){
		
	
		var p = new KuberntesMetaModel::Port();
		p.number := port.oclAsType(kuberProfile::DataTypes::Port).port;
		p.name := port.oclAsType(kuberProfile::DataTypes::Port).portName;
		ports += p;
	};
	if(ports->notEmpty()){
		return ports->asOrderedSet();
	};
	
	return null;
}
helper UML::Node::getMemoryMount(in deploymentStereoType : UML::Stereotype): String {
	var memoryMount := self.getValue(deploymentStereoType,"memoryMount").oclAsType(kuberProfile::DataTypes::MemoryMount);
	
	if(memoryMount <> null){
		

		return memoryMount.mountPath;
	};
	return null;
}
helper UML::Node::getCommand(in deploymentStereoType : UML::Stereotype): String{
	var command := self.getValue(deploymentStereoType,"command");
	
	if(command <> null){
		return command.oclAsType(String);
	};
	return null;
}

helper UML::Node::getArguments(in deploymentStereoType : UML::Stereotype): OrderedSet(String){

	var returnArgs: Set(String);
	self.getValue(deploymentStereoType,"args")->flatten()->asSequence()->forEach(variable){
			log(variable.toString());
			returnArgs += variable.oclAsType(String);
	};
	if(returnArgs->notEmpty()){
		return returnArgs->asOrderedSet();
	};
	
	return null;
}

helper UML::Node::getSecurityContext(in deploymentStereoType : UML::Stereotype): SecurityContext{
	var securityContext := self.getValue(deploymentStereoType,"securityContext").oclAsType(kuberProfile::DataTypes::SecurityContext);
	
	if(securityContext <> null){
		var returnSecurityContext = new SecurityContext();
	
	
		returnSecurityContext.runAsUser := securityContext.runAsUser;
		returnSecurityContext.runAsRoot := securityContext.runAsRoot;
		returnSecurityContext.runAsGroup := securityContext.runAsGroup;
		returnSecurityContext.fsGroup := securityContext.fsGroup;
		returnSecurityContext.readOnlyRootFilesystem := securityContext.readOnlyRootFilesystem;
	
		returnSecurityContext.capabilities := securityContext.getCapabilities();
		return returnSecurityContext;
	};
	return null;

}
helper kuberProfile::DataTypes::SecurityContext::getCapabilities() : Capabilities{
	
	
	var cap = new Capabilities();
	self->capabilities.add->forEach(add) {	
		cap.add += add;
	};
	self->capabilities.drop->forEach(drop) {	
		cap.drop += drop;
	};
	return cap;
}

helper UML::Node::getResourceAllocation(in deploymentStereoType : UML::Stereotype) : ResourceAllocation {
	var resourceSpecification := self.getValue(deploymentStereoType, "resourceSpecification").oclAsType(kuberProfile::DataTypes::ResourceSpecification);
	var limit := resourceSpecification.limit;
	var request := resourceSpecification.request;
	



	if(limit.cpuUtalization > 0 and limit.memory > 0 and request.cpuUtalization > 0 and request.memory > 0){
		
		var returnResourceAlloc := new ResourceAllocation();
 		returnResourceAlloc.cpuLimit :=(limit.cpuUtalization * 1000).floor().toString() + "m";
		returnResourceAlloc.cpuRequest :=(request.cpuUtalization * 1000).floor().toString() + "m";
	
		returnResourceAlloc.memoryLimit := limit.memory.toString() + "Mi";
		returnResourceAlloc.memoryRequest := request.memory.toString() + "Mi";
		return returnResourceAlloc;
	};


	return null;
}
helper UML::Node::getEnviromentVariables(in deploymentStereoType : UML::Stereotype) : OrderedSet(EnviromentVariables){

	var environmentVariables :=	self.getValue(deploymentStereoType,"environmentVariables"); //.oclAsType(KuberProfile2::EnviromentVariables);
	var nodeEnviromentVars: Set(EnviromentVariables);
	
	
	environmentVariables->flatten()->asSequence()->forEach(variable){	
	 //log("found environment variable ToString is:" + variable.toString() + " and repr is: " + variable.repr());
	    var name := variable.oclAsType(kuberProfile::DataTypes::EnviromentVariables).name;
	    var value := variable.oclAsType(kuberProfile::DataTypes::EnviromentVariables).value;
	    //log("found environment variable: name-value are:" + name.toString()+"-"+value.toString());
	  	
		nodeEnviromentVars +=createEnviromentVariable(name,value);              
	};	
	return nodeEnviromentVars->asOrderedSet();
}


helper UML::Node::getDataPersistent(in deploymentStereoType : UML::Stereotype) : PersistentData {
	
	var dataPersistent :=	self.getValue(deploymentStereoType,"dataPersistent");
	if(dataPersistent <> null){
		var size := dataPersistent.oclAsType(kuberProfile::DataTypes::PersistentData).sizeInGB;
		var mountPath := dataPersistent.oclAsType(kuberProfile::DataTypes::PersistentData).mountPath;
		var pd := new PersistentData();
		pd.mountPath := mountPath;
		pd.sizeInGB := size;
		return pd;
	
	};	
	return null;
}

